plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.georgeakulov'
version = '1.2.2'

repositories {
    mavenLocal()
    mavenCentral()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        // Create a publication named 'mavenJava'
        mavenJava(MavenPublication) {
            // Publish the 'java' component (production JAR, sources, and metadata)
            from components.java

            pom {
                name.set("Json schema implementation")
                description.set("Json schema Draft7+")
                url.set("https://github.com/georgeakulov/json-schema")
                licenses {
                    license {
                        name.set("The Apache License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                    }
                }
                developers {
                    developer {
                        id.set("json-schema")
                        name.set("George Akulov")
                        email.set("9421755@gmail.com")
                    }
                }
                scm {
                    connection.set("scm:https://github.com/georgeakulov/json-schema.git")
                    url.set("https://github.com/georgeakulov/json-schema")
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            var releasesRepoUrl = uri("https://s01.oss.sonatype.org")
            var snapshotsRepoUrl = uri("https://s01.oss.sonatype.org")
            url = version.toString().endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("ossrhUsername")
                password = System.getenv("ossrhPassword")
            }
        }
    }
}

dependencies {

    implementation("com.fasterxml.jackson.core:jackson-databind:2.20.1")
    api("io.projectreactor:reactor-core:3.8.1")

    testImplementation platform('org.junit:junit-bom:6.0.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    minHeapSize = '3G'
    maxHeapSize = '3G'
    jvmArgs '-XX:+HeapDumpOnOutOfMemoryError'
}